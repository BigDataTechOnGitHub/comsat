sourceSets {
    jdk7 {
        java {
            srcDir 'src/main/java'
        }
            
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
    jdk8 {
        java {
            srcDir 'src/main/java'
        }
            
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

compileJdk7Java {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

compileJdk8Java {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    jdk7Compile "co.paralleluniverse:quasar-core:$quasarVer"
    jdk8Compile "co.paralleluniverse:quasar-core:$quasarVer:jdk8"
    compile "org.eclipse.jetty:jetty-webapp:$jettyVer"
    testCompile (project(':comsat-test-utils')) {
        exclude group: 'org.apache.tomcat.embed', module: '*'         
    }
    testRuntime "org.slf4j:slf4j-log4j12:$slf4jApiVer"
    testRuntime "log4j:log4j:$log4jVer"
    testRuntime project(':comsat-jdbc')
}

def ssets = [sourceSets.jdk7]
if(ext.java8) {
    ssets += sourceSets.jdk8
}
ssets.each { set ->
    def jarTask = task("${set.name}Jar", type: Jar) {
        from set.output
        from { project.configurations["${set.name}Runtime"].collect { it.isDirectory() ? it : zipTree(it) } }
    } 

    artifacts {
        archives jarTask
    }

    assemble.dependsOn jarTask
}

if (ext.java8) {
    jdk8Jar {
        classifier = 'jdk8'
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task deployWar(type: Copy) {
    evaluationDependsOn ":comsat-test-war"
    from project(":comsat-test-war").war
    into "build/wars"
    rename (".*war","dep.war")
}

tasks.withType(Test) {
    dependsOn deployWar
}
