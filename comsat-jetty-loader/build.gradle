apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile "co.paralleluniverse:quasar-core:$quasarVer"
    provided "org.eclipse.jetty:jetty-webapp:$jettyVer"
    testCompile (project(':comsat-test-utils')) {
        exclude group: 'org.apache.tomcat.embed', module: '*'         
    }
    testRuntime "org.slf4j:slf4j-log4j12:$slf4jApiVer"
    testRuntime "log4j:log4j:$log4jVer"
    testRuntime project(':comsat-jdbc')
}

shadowJar {
    outputs.upToDateWhen { false }

    // artifactAttached = false
    classifier = 'shadow'
    destinationDir = file("$buildDir/libs")

    dependencies {
        include(dependency('co.paralleluniverse:quasar-core'))
    }

    doLast {
        logger.info("$buildDir/libs/${project.name}-${project.version}-${classifier}.jar -> $buildDir/libs/${project.name}-${project.version}.jar")
        file("$buildDir/libs/${project.name}-${version}-${classifier}.jar").renameTo(file("$buildDir/libs/${project.name}-${project.version}.jar"))
    }
}

shadowJar.dependsOn jar
build.dependsOn shadowJar
install.dependsOn shadowJar
signArchives.dependsOn shadowJar
uploadArchives.dependsOn shadowJar

if (System.properties['java.version'].startsWith('1.8') ) { 
    artifacts {
        archives jar
        archives project(':comsat-jetty-loader-jdk8').jar
        archives sourcesJar
        archives javadocJar
        //    archives distZip
    }

    install.dependsOn ':comsat-jetty-loader-jdk8:shadowJar'
    uploadArchives.dependsOn ':comsat-jetty-loader-jdk8:shadowJar'
}

task deployWar(type: Copy) {
    evaluationDependsOn ":comsat-test-war"
    from project(":comsat-test-war").war
    into "build/wars"
    rename (".*war","dep.war")
}

tasks.withType(Test) {
    dependsOn deployWar
}
