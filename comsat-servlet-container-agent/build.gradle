// Enabling shadow plugin
buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
  }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

// Shadowed dependencies must be listed
dependencies {
    runtime "co.paralleluniverse:quasar-core:$quasarVer"
    runtime "org.ow2.asm:asm:$asmVer"
    runtime "org.ow2.asm:asm-analysis:$asmVer"
    runtime "org.ow2.asm:asm-commons:$asmVer"
    runtime "org.ow2.asm:asm-util:$asmVer"
    runtime "org.ow2.asm:asm-tree:$asmVer"
}

jar {
    classifier = ''

    // Quasar manifest
    manifest {
        attributes (
            "Built-By"                  :   System.getProperty("user.name"),
            "Implementation-Title"      :   project.name,
            "Implementation-Version"    :   version,
            "Implementation-Vendor"     :   vendor,
            "Premain-Class"             :   "co.paralleluniverse.fibers.instrument.JavaAgent",
            "Agent-Class"               :   "co.paralleluniverse.fibers.instrument.JavaAgent",
            "Can-Retransform-Classes"   :   "true",
            "Can-Redefine-Classes"      :   "true",
        )
    }
}

jar.enabled = false

// TODO: this will issue a warning about non-existing archives, find a way to fix by overriding the inherited "subprojects"
// configuration of "artifacts" so that it only installs/uploads the main jar

sourcesJar.enabled = false
javadocJar.enabled = false

shadowJar {
    classifier = ''
    
    // Relocation in order to avoid version conflicts
    relocate 'org.objectweb.asm', 'reloc.co.paralleluniverse.comsat.agent.shadows.org.ow2.asm'
    
    // Shadowed dependencies must be listed to enable whitelist mode
    dependencies {
        include(dependency("co.paralleluniverse:quasar-core:$quasarVer"))
        include(dependency("org.ow2.asm:asm:$asmVersion"))
        include(dependency("org.ow2.asm:asm-analysis:$asmVersion"))
        include(dependency("org.ow2.asm:asm-commons:$asmVersion"))
        include(dependency("org.ow2.asm:asm-util:$asmVersion"))
        include(dependency("org.ow2.asm:asm-tree:$asmVersion"))
    }
}

// Always upload JDK8, no need for conditional on local JDK, nothing to be compiled
artifacts {
    archives shadowJar
    archives project(':comsat-servlet-container-agent-jdk8').jar
}

// Dependency on JDK8 version
install.dependsOn ':comsat-servlet-container-agent-jdk8:shadowJar'
uploadArchives.dependsOn ':comsat-servlet-container-agent-jdk8:shadowJar'
